plugins {
    id "java"
    id "io.github.spencerpark.jupyter-kernel-installer" version "2.1.0" // <1>
}

group 'io.github.spencerpark' // <2>
version '1.0.0-SNAPSHOT'

sourceCompatibility = 1.8

wrapper {
    gradleVersion = '4.8.1'
    distributionType = Wrapper.DistributionType.ALL
}

repositories {
    mavenLocal()
    mavenCentral()
}

configurations {
    shade
    shade.transitive = true
    compile.extendsFrom(shade)
}

dependencies {
    shade group: 'io.github.spencerpark', name: 'jupyter-jvm-basekernel', version: '2.3.0' // <3>
}

jar {
    // Include all shaded dependencies in the jar
    from configurations.shade
            .collect { it.isDirectory() ? it : zipTree(it) }

    manifest {
        attributes('Main-class': 'io.github.spencerpark.jupyter.INashornJS') // <4>
    }
}

jupyter { // <5>
    kernelName = 'nashorn-js'
    kernelDisplayName = 'NashornJS'
    kernelLanguage = 'js'
    kernelInterruptMode = 'message'
    kernelParameters {
        list('engine-arg', 'JS_ENGINE_ARGS') {
            separator = ' '
            description = 'A comma separated list of arguments to give to the scripting engine.'
        }
    }
}

zipKernel {
    installers {
        with 'python' // <6>
    }
}